plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'dagger.hilt.android.plugin'
}

android {

    signingConfigs {
        release {
            // Place file release.keystore in app directory
            // To generate new keystore use Keystore explorer (Win) - https://keystore-explorer.org/downloads.html

            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    compileSdk 34

    defaultConfig {
        applicationId 'by.egleba.studentbsu'
        minSdk 26
        targetSdk 34
        versionCode 12
        versionName "1.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        proguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                optimizeCode true
                obfuscate true
                removeUnusedResources true
            }

            // TODO: See issue #15 https://github.com/geugenm/student-bsu-by/issues/15
            minifyEnabled false
            shrinkResources false

            signingConfig signingConfigs.release
        }
        debug {
            jniDebuggable true
            renderscriptDebuggable true

            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    configurations {
        configureEach {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.3"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    ndkVersion '25.1.8937393'
    namespace 'github.alexzhirkevich.studentbsuby'
}

dependencies {
    // AndroidX Core and UI
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.android.material:material:1.12.0'

    // Jetpack Compose
    implementation 'androidx.compose.ui:ui:1.7.4'
    implementation 'androidx.compose.material:material:1.7.4'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.7.4'
    implementation 'androidx.compose.material:material-icons-extended:1.7.4'

    // AndroidX Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.6'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.6'

    // AndroidX Activity Compose
    implementation 'androidx.activity:activity-compose:1.9.3'

    // Retrofit and OkHttp
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.github.tomasharkema:RetrofitCurlPrinter:0.0.2'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:19.0.1'

    // Navigation Compose
    implementation 'androidx.navigation:navigation-runtime-ktx:2.8.3'
    implementation 'androidx.navigation:navigation-compose:2.8.3'

    // Preferences
    implementation "androidx.preference:preference-ktx:1.2.1"

    // Cookie Management
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Dependency Injection (Dagger Hilt)
    implementation 'com.google.dagger:hilt-android:2.52'
    ksp 'com.google.dagger:hilt-compiler:2.51.1' // As for 22.10.2024 updating this breaks the compilation
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    // HTML Parsing
    implementation 'org.jsoup:jsoup:1.18.1'

    // Room Database
    implementation 'androidx.room:room-ktx:2.6.1'
    ksp 'androidx.room:room-compiler:2.6.1'

    // Toolbar Compose
    implementation 'me.onebone:toolbar-compose:2.3.5'

    // HTML Text Compose
    implementation 'de.charlex.compose:html-text:1.6.0'

    // Accompanist Libraries
    implementation 'com.google.accompanist:accompanist-pager:0.36.0'
    implementation 'com.google.accompanist:accompanist-pager-indicators:0.36.0'
    implementation 'com.google.accompanist:accompanist-insets-ui:0.36.0'
    implementation "com.google.accompanist:accompanist-insets:0.31.5-beta"
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.36.0'

    // Image Loading (Glide)
    implementation 'com.github.skydoves:landscapist-glide:2.4.1'

    // WorkManager
    implementation 'androidx.work:work-runtime-ktx:2.9.1'
    implementation 'androidx.hilt:hilt-work:1.2.0'

    // Security
    implementation("androidx.security:security-crypto:1.1.0-alpha06")

    // Other Libraries
    implementation 'io.github.wanghonglin:crom-app-whitelist:0.1.2'
    implementation 'com.github.mintrocket.MintPermissions:mintpermissions:1.1.2'
}
